<Program> ::=
    <Initialize>
    <HadamardAll>
    <OracleBlock>
    <FinalHadamard>
    <Measure>

<Initialize> ::=
    "qc = QuantumCircuit(2, 2)\n"
    "## Initialize circuit\n"
    "qc.x(1)\n"

<HadamardAll> ::=
    "## Apply Hadamard on qubits 0 and 1\n"
    "qc.h(0)\n"
    "qc.h(1)\n"

<OracleBlock> ::=
    "## Begin Oracle\n"
    <OracleGateList>
    "## End Oracle\n"

<OracleGateList> ::=
    <OracleGate>
  | <OracleGate> <OracleGateList>

<OracleGate> ::=
  <SingleQubitGate>
  | <TwoQubitGate>
  | <ParameterizedGate>

<FinalHadamard> ::=
    "## Apply final Hadamard on qubit 0 only\n"
    "qc.h(0)\n"

<Measure> ::=
    "## Measure both qubits\n"
    "qc.measure(0,0)\n"
    "qc.measure(1,1)\n"

<SingleQubitGate> ::=
    "qc.x(" <QubitSingle> ")\n"
  | "qc.y(" <QubitSingle> ")\n"
  | "qc.z(" <QubitSingle> ")\n"
  | "qc.h(" <QubitSingle> ")\n"
  | "qc.s(" <QubitSingle> ")\n"
  | "qc.sdg(" <QubitSingle> ")\n"
  | "qc.t(" <QubitSingle> ")\n"
  | "qc.tdg(" <QubitSingle> ")\n"
  | "qc.id(" <QubitSingle> ")\n"

<TwoQubitGate> ::=
    "qc.cx(" <TwoDistinctQubits> ")\n"
  | "qc.cy(" <TwoDistinctQubits> ")\n"
  | "qc.cz(" <TwoDistinctQubits> ")\n"
  | "qc.swap(" <TwoDistinctQubits> ")\n"

<ParameterizedGate> ::=
    "qc.rx(" <Angle> "," <QubitSingle> ")\n"
  | "qc.ry(" <Angle> "," <QubitSingle> ")\n"
  | "qc.rz(" <Angle> "," <QubitSingle> ")\n"
  | "qc.u(" <Angle> "," <Angle> "," <Angle> "," <QubitSingle> ")\n"
  | "qc.rxx(" <Angle> "," <TwoDistinctQubits> ")\n"
  | "qc.ryy(" <Angle> "," <TwoDistinctQubits> ")\n"
  | "qc.rzz(" <Angle> "," <TwoDistinctQubits> ")\n"
  | "qc.rzx(" <Angle> "," <TwoDistinctQubits> ")\n"

<QubitSingle> ::= "0" | "1"

<TwoDistinctQubits> ::= "0,1" | "1,0"

<Angle> ::=
    "0"
  | "np.pi/4"
  | "np.pi/2"
  | "np.pi"
  | "3*np.pi/2"
  | "2*np.pi"
  | "0.5"
  | "1.3"
  | "2.7"
  | "0.314"
  | "1.5708"
  | "3.1415"